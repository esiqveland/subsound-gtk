import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.gradleup.shadow' version '9.3.1'
    id 'java'
    id 'application'
    id 'io.github.jwharm.flatpak-gradle-generator' version '1.6.0'
    //    id 'androidx.room' version "2.7.0-alpha06" apply false
}

group = 'com.github.subsounds'
version = '0.1.0'
def mainClass = 'com.github.subsound.Main'

repositories {
    maven { url './offline-repository/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
    mavenCentral()
    google()
}

def javagiVersion = "0.14.0-RC1"
def recordBuilderVersion = "44"

dependencies {
    implementation "org.java-gi:gtk:$javagiVersion"
    implementation "org.java-gi:adw:$javagiVersion"
    implementation "org.java-gi:gstreamer:$javagiVersion"
    implementation "org.java-gi:secret:$javagiVersion"
    implementation("org.xerial:sqlite-jdbc:3.50.3.0")

    implementation 'net.beardbot:subsonic-client:0.6.0'
    implementation "com.github.ben-manes.caffeine:caffeine:3.2.2"
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'de.androidpit:color-thief:1.1.2'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.10'
    implementation 'com.github.akarnokd:rxjava3-fiber-interop:0.0.18'
    compileOnly 'com.github.akarnokd:rxjava3-jdk9-interop:3.0.1'
    implementation 'com.softwaremill.jox:channels:1.0.0'
    implementation 'com.github.hypfvieh:dbus-java-core:5.1.0'
    implementation 'com.github.hypfvieh:dbus-java-transport-native-unixsocket:5.1.0'

    annotationProcessor "io.soabase.record-builder:record-builder-processor:$recordBuilderVersion"
    compileOnly "io.soabase.record-builder:record-builder-core:$recordBuilderVersion"

    implementation 'org.jspecify:jspecify:1.0.0'

    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'org.slf4j:jul-to-slf4j:2.0.16'
    implementation 'org.slf4j:jul-to-slf4j:2.0.16'
    implementation 'ch.qos.logback:logback-classic:1.5.18'

    implementation 'com.twelvemonkeys.imageio:imageio-core:3.12.0'
    implementation 'com.twelvemonkeys.imageio:imageio-metadata:3.12.0'
    // webp conversion support:
    implementation 'com.twelvemonkeys.imageio:imageio-webp:3.12.0'

    testImplementation 'org.assertj:assertj-core:3.27.3'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.15.2'
}

// copy the Constants class and replace the version placeholder with the application version
tasks.register("processVersion", Copy) {
    from("src/main/java/com/github/subsound/configuration/constants/") {
        include("Constants.java")
        filter(ReplaceTokens, tokens: ['version': version])
    }
    into layout.buildDirectory.dir("generated/sources/classes/src/java/main/com/github/subsound/configuration/constants/")

    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

// exclude the original Constants class from the source set so gradle doesn't compile it in
sourceSets {
    main {
        java {
            exclude ("com/github/subsound/configuration/constants/Constants.java")
        }
        resources {
            exclude("samples/*")
        }
    }
}

// include the new Constants class that was previously generated
tasks.named("compileJava", JavaCompile) {
    dependsOn("processVersion")
    source(layout.buildDirectory.dir("generated/sources/classes/src/java/main/com/github/subsound/configuration/constants/"))
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

test {
    jvmArgs += "--enable-native-access=ALL-UNNAMED"
    useJUnit()

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
    afterTest { desc, result ->
        def output = "Class name: ${desc.className}, Test name: ${desc.name},  (Test status: ${result.resultType})"
        println( '\n' + output)
    }

    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
}

tasks.named('run') {
    //jvmArgs += "-XstartOnFirstThread"
    jvmArgs += "--enable-native-access=ALL-UNNAMED"
    jvmArgs += "-Djava.library.path=/usr/lib64:/lib64:/lib:/usr/lib:/lib/x86_64-linux-gnu "
    //jvmArgs += "-Djava.library.path=/usr/lib64:/lib64:/lib:/usr/lib:/lib/x86_64-linux-gnu:/opt/homebrew/lib "
}

tasks.flatpakGradleGenerator {
    outputFile = file('flatpak-sources.json')
    downloadDirectory = './offline-repository'
}

application {
    setMainClass(mainClass)
}

shadowJar {
    archiveClassifier.set('final')
}

tasks.jar {
    manifest {
        attributes["Main-Class"] = mainClass;
    }
    dependsOn("shadowJar")
}
